buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

apply plugin: 'forge'

version				= config.mod_ver
group				= config.mod_group
archivesBaseName	= config.mod_name

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = "${config.mc_ver}-${config.forge_ver}"
	runDir = "eclipse"
}

repositories {
	mavenLocal()
	maven {
		name = "chickenbones"
		url = "http://chickenbones.net/maven"
	}
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to"
	}
}

dependencies {
	compile "cn.annoreg:AnnotationRegistry:${config.annoreg_ver}:dev"
	compile "cn.liutils:LIUtils:${config.liutils_ver}:dev"
	compile "codechicken:NotEnoughItems:1.7.10-1.0.3.74:dev"
	compile "net.industrial-craft:industrialcraft-2:2.2.717-experimental:dev"
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task deobfJar(type: Jar, dependsOn: 'jar') {
	classifier 'dev'
	from sourceSets.main.output
}

artifacts {
	archives deobfJar
}

task getLibs << {
	ant.mkdir(dir: 'libs')
	ant.get(src: 'http://addons-origin.cursecdn.com/files/2246/932/ThermalExpansion-[1.7.10]4.0.3B1-218-dev.jar', dest: 'libs/ThermalExpansion-[1.7.10]4.0.3B1-218-dev.jar', skipexisting: 'true')
}

sourceMainJava.dependsOn getLibs
install.dependsOn build

